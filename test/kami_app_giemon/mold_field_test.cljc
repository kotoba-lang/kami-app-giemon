(ns kami-app-giemon.mold-field-test
  (:require [clojure.test :refer [deftest testing is]]
            [kami-app-giemon.mold-field :as mf]))

;; 1:1 port of `mold_field.rs`'s `scrub_is_local_and_bounded` test.
(deftest scrub-is-local-and-bounded
  (let [f0 (mf/new-field [0.0 0.0] 0.1 10 10 1.0)
        total0 (mf/total-coverage f0)]
    (is (< (Math/abs (- total0 100.0)) 1.0e-3) "100 cells x 1.0")
    (let [[f1 removed] (mf/scrub f0 0.5 0.5 0.25 1.0)]
      (is (> removed 0.0) "should remove some mold")
      (is (< (mf/total-coverage f1) total0) "total coverage decreases")
      (is (= (mf/coverage-at f1 0.05 0.05) 1.0) "corner stays full")
      (is (every? #(>= % 0.0) (:coverage f1)) "no negative coverage")
      (let [f-final (reduce (fn [f _] (first (mf/scrub f 0.5 0.5 0.25 1.0)))
                             f1 (range 50))]
        (is (<= (mf/coverage-at f-final 0.5 0.5) 1.0e-6) "centre fully cleaned")
        (is (every? #(>= % 0.0) (:coverage f-final)))))))

;; 1:1 port of `mold_field.rs`'s `cell_index_maps_world_to_grid` test.
(deftest cell-index-maps-world-to-grid
  (let [f (mf/new-field [-0.2 -0.3] 0.05 8 6 1.0)]
    (is (= 0 (mf/cell-index f (+ -0.2 0.01) (+ -0.3 0.01))))
    (is (nil? (mf/cell-index f -0.5 0.0)) "left of grid")
    (is (nil? (mf/cell-index f 10.0 10.0)) "right of grid")))

(deftest scrub-no-op-when-non-positive
  (let [f (mf/new-field [0.0 0.0] 0.1 5 5 1.0)
        [f' removed] (mf/scrub f 0.25 0.25 0.1 0.0)]
    (is (= 0.0 removed))
    (is (= f f'))))
